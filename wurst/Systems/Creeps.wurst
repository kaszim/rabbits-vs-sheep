package Creeps
import ClosureTimers
import FText
import Config
import GameState
import Creep
import Team
import ClosureEvents


public class Creeps
    private int array[2] creepCount

    construct()
        creepCount[Team.RABBITS.toInt()] = 0 // number of creeps the rabbits own
        creepCount[Team.SHEEP.toInt()] = 0
        // so text is number of creeps rabbits own => display on sheep side
        var text = createFText(Team.SHEEP.rect().getCenter().toVec3(), "0", CREEP_COUNT_TEXT_SIZE, 0., ZERO2, colorA(0, 255, 0, 255))
        var text2 = createFText(Team.RABBITS.rect().getCenter().toVec3(), "0", CREEP_COUNT_TEXT_SIZE, 0., ZERO2, colorA(0, 255, 0, 255))
        doPeriodically(WIN_CONDITION_FREQUENCY) cb ->
            let rabbits = creepCount[Team.RABBITS.toInt()]
            let sheep = creepCount[Team.SHEEP.toInt()]
            let k = 255/MAX_CREEP
            text.tt
            ..setText(rabbits.toString(), CREEP_COUNT_TEXT_SIZE)
            ..setColor((rabbits*k).toInt(), 255-(rabbits*k).toInt(), 0, 255)
            text2.tt
            ..setText(sheep.toString(), CREEP_COUNT_TEXT_SIZE)
            ..setColor((sheep*k).toInt(), 255-(sheep*k).toInt(), 0, 255)
    
    function getCreepCount(Team t) returns int
        return creepCount[t.toInt()]

    function add(Team t)
        add(t, t.oppositeTeam().rect().randomPoint())

    function add(Team t, vec2 pos)
        let creep = createUnitZ(t.creepController(), getCreep(t), pos.toVec3(), angle(0))
        EventListener.add(creep, EVENT_PLAYER_UNIT_DEATH) ->
            onCreepDeath(creep)
        creepCount[t.toInt()]++
    
    function getCreep(Team t) returns int
        if t == Team.RABBITS
            return RABBIT
        return SHEEP
    
    function clear()
        creepCount[0] = 0
        creepCount[1] = 0

    private function onCreepDeath(unit creep)
        if gameState == GameState.PLAYING
            for i = 0 to CREEP_MULTIPLIER
                add(creep.getOwner().getEnumTeam().oppositeTeam())

        creepCount[creep.getOwner().getEnumTeam().toInt()]--
        creep.remove()
