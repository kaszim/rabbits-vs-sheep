package Creep
import ObjectIdGenerator
import UnitObjEditing
import EntityManagement
import Team
import SoundUtils
import Assets
import ClosureTimers
import Config

constant RABBIT = compiletime(UNIT_ID_GEN.next())
constant SHEEP = compiletime(UNIT_ID_GEN.next())

@compiletime function createRabbit()
	new UnitDefinition(RABBIT, 'necr')
	..setName("Amazing Rabbit")
	..setCreepFeatures()

@compiletime function createSheep()
	new UnitDefinition(SHEEP, 'nshe')
	..setName("Amazing Sheep")
	..setCreepFeatures()

@compiltime function UnitDefinition.setCreepFeatures()
	this
	..setHitPointsMaximumBase(100)
	..setDeathType(3) // Can Raise, Does Decay
	..setDefenseBase(3)
	..setNormalAbilities("")
	..setAttack1CooldownTime(1.35)
	..setAttack1DamageBase(11)
	..setAttack1DamageNumberofDice(1)
	..setAttack1DamageSidesperDie(2)
	..setAttack1Range(90)
	..setAttack1RangeMotionBuffer(250.0) 
	..setAttack1TargetsAllowed("Ground")
	..setAttack1WeaponType(WeaponType.Normal)
	..setAttacksEnabled(1)
	..setAcquisitionRange(500.)
	..setLevel(2)
	..setGoldBountyAwardedBase(90)
	..setGoldBountyAwardedNumberofDice(6)
	..setGoldBountyAwardedSidesperDie(6)
	..setSpeedBase(100)
	..setSpeedMaximum(100)
	..setSpeedMinimum(100)

function getCreep(Team t) returns int
	if t == Team.RABBITS
		return RABBIT
	return SHEEP

public class Creep extends UnitEntity
	private static SoundDefinition deathSound = new SoundDefinition(Sounds.artilleryCorpseExplodeDeath1, false, true)
	private Team team

	construct(vec2 pos, Team t)
		super(createUnitZ(t.creepController(), getCreep(t), pos.toVec3(), angle(0)))
		sleeps = false
		team = t
		EventListener.add(actor, EVENT_PLAYER_UNIT_DEATH) ->
			flashEffect(Objects.humanLargeDeathExplode, this.pos)
			deathSound.playOnPoint(this.pos)
		//TODO: Add Creep ai
	
	function getTeam() returns Team
		return team
