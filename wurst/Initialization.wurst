package Initialization
import EntityManagement
import ClosureTimers
import GameState
import Game
import Team
import Config
import Scoreboard
import Assets

Game game
Scoreboard sb


function initScoreboard()
	sb = new Scoreboard(Team.RABBITS.getPlayers().size(), Team.SHEEP.getPlayers().size(), 1)
	sb.setTeamRow(0, "|c00EEEE00" + Team.RABBITS.toString(), Icons.bTNCritterRabbit)
	sb.setTeamRow(1, "|c00EEEE00" + Team.SHEEP.toString(), Icons.bTNSheep)
	game.registerOnRoundPrepare() ->
		sb.setRows(Team.RABBITS.toInt(), heroesToScoreboardRows(Team.RABBITS))
		sb.setRows(Team.SHEEP.toInt(), heroesToScoreboardRows(Team.SHEEP))
	doPeriodically(0.1) cb ->
		for p in Team.RABBITS.getPlayers()
			sb.setData(p.getId() + 1, 1, "|c00EE1000Kills: " + p.getGold().toString())
		var i = 0
		for p in Team.SHEEP.getPlayers()
			sb.setData(Team.RABBITS.getPlayers().size() + 2 + i, 1, "|c00EE1000Kills: " + p.getGold().toString())
			i++
	sb.display()

function heroesToScoreboardRows(Team t) returns LinkedList<ScoreboardRow>
	LinkedList<ScoreboardRow> rows = new LinkedList<ScoreboardRow>()
	for hero in game.getHeroes(t)
		rows.add(new ScoreboardRow(hero.getIcon(),  hero.actor.getOwner().getNameColored()))
	return rows

init
	FogMaskEnable(false)
	FogEnable(false)
	SetCameraField(CAMERA_FIELD_TARGET_DISTANCE, 2400, 0.00)
	SetCameraField(CAMERA_FIELD_FARZ, 100000000.00, 0)
	gameState = GameState.SETTINGS
	game = new Game()
	startEntityLoop()
	SetPlayerState(RABBITS_PLAYER, PLAYER_STATE_GIVES_BOUNTY, 1)
	SetPlayerState(SHEEP_PLAYER, PLAYER_STATE_GIVES_BOUNTY, 1)
	initializeTeams()
	doAfter(1.) ->
		initScoreboard()
		game.start()

