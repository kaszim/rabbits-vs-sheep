package CrushingWeight

import Assets
import public BuffObjEditing
import ChannelAbilityPreset
import RegisterEvents
import ClosureTimers
import ClosureForGroups

constant SPELL_NAME = "Crushing Weight"
constant BUFF_NAME = "Being crushed"
constant BUFF_TT = "This unit is currently being crushed by a wrecking ball"
constant SPELL_TT_NORMAL = "Because of its humongous weight, the Wrecking Ball crushes all creeps"
constant SPELL_TT_EXTENDED = SPELL_TT_NORMAL

public let SPELL_ID = compiletime(ABIL_ID_GEN.next())
public let BUFF_OBJ = compiletime(createDummyBuffObject(BUFF_NAME, BUFF_TT,	Icons.bTNFireBolt, Abilities.curseTarget, "chest"))

@compiletime function genObj()
    new AbilityDefinitionAttributeModifierSkill(SPELL_ID)
    ..setLevels(3)
    ..setName(SPELL_NAME)
    ..setTooltipLearn(SPELL_TT_NORMAL)
    ..setTooltipLearnExtended(SPELL_TT_NORMAL)
    ..setHotkeyLearn("E")
    ..presetIcon(Icons.pASBTNThornShield)
    ..presetTooltipNormal(lvl -> SPELL_TT_NORMAL)
    ..presetTooltipNormalExtended(lvl -> SPELL_TT_EXTENDED)
    ..presetTooltipLearn(lvl -> SPELL_TT_NORMAL)
    ..presetTooltipLearnExtended(lvl -> SPELL_TT_NORMAL)
    ..presetButtonPosNormal(2, 2)
    ..presetButtonPosResearch(2, 0)
    ..presetManaCost(lvl -> 0)
    ..presetCooldown(lvl -> 0.)
    ..presetAgilityBonus(lvl -> 0)
    ..presetStrengthBonus(lvl -> 0)
    ..presetIntelligenceBonus(lvl -> 0)

init
    registerPlayerUnitEvent(EVENT_PLAYER_HERO_SKILL, null, () -> GetLearnedSkill() == SPELL_ID) ->
        let u = GetTriggerUnit()
        u.addAbility(SPELL_ID)
        doPeriodically(0.1) cb ->
            let pos = u.getPos()
            forUnitsInRange(pos, 30) u2 ->
                if u2 != u
                    u.damageTarget(u2, 10)
                    flashEffect(Objects.critterBloodAlbatross, pos)